title In the Belly of the Beast
author Taylor Anderson
homepage www.puzzlescript.net
========
OBJECTS
========

Background
black

Wall
purple
.000.
00000
00000
00000
.000.

Player
orange white
.....
10001
10001
11011
.1.1.

InactivePlacementMarker
black
00.00
0...0
.....
0...0
00.00

PlacementMarker
white
00.00
0...0
.....
0...0
00.00

Crate
red white
.000.
00.00
0.0.0
00.00
.000.

TempCrate
black

Vein
black darkred
.111.
10011
00000
00111
11..1

BloodCellTop
white blue lightblue
.000.
02120
01010
02120
.000.

BloodCellBottom
white blue lightblue
.000.
02120
01010
02120
.000.

DeadBloodCell
white blue lightblue
.000.
02120
01010
02120
.000.


CellSpawnerTop
black

CellSpawnerBottom
black

Null
transparent

CellKiller
black

Virus1
green darkgreen
.0.0.
01000
.010.
00010
.0.0.

Virus3
green darkgreen
0.0.0
.000.
01110
.000.
0.0.0

Virus2
green darkgreen
..00.
0001.
00100
.1000
.00..


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
b = BloodCellBottom
t = BloodCellTop
y = CellSpawnerTop
n = CellSpawnerBottom
s = Vein
x = CellKiller
K = Vein and CellSpawnerTop
L = Vein and CellSpawnerBottom
H = Vein and CellKiller
g = Virus1
@ = InactivePlacementMarker

Obstacle = Wall or Crate or DeadBloodCell

Virus = Virus1 or Virus2 or Virus3
BloodCell = BloodCellTop or BloodCellBottom
PotentialCellTop = BloodCellTop or Null
PotentialCellBottom = BloodCellBottom or Null

=======
SOUNDS
=======


================
COLLISIONLAYERS
================
Null, TempCrate
InactivePlacementMarker
Background
CellKiller, CellSpawnerTop, CellSpawnerBottom
Vein
BloodCellTop, BloodCellBottom, DeadBloodCell
PlacementMarker
Player, Wall, Crate, Virus


======
RULES
======

(toggling selection mode)
[ action Player TempCrate] -> [Player]
[ action Player ] -> [Player TempCrate]

(placing crates)
[> Player TempCrate] -> [Player > TempCrate]
late [TempCrate Crate] -> []
late [TempCrate Virus] -> [Virus]
late [TempCrate no Player] -> [Crate]



(showing placement markers around player when action is pressed)
[ moving Player |  ] -> [ moving Player | moving InactivePlacementMarker ]

late [Player TempCrate | InactivePlacementMarker] -> [Player TempCrate | PlacementMarker]
late [Player no TempCrate | PlacementMarker] -> [Player no TempCrate | InactivePlacementMarker]

(unsure why this works, just leave it alone)
[> Player | Crate PlacementMarker] -> [Player | PlacementMarker]

(spawning bloodcells)
late [CellSpawnerTop no BloodCellTop] -> [CellSpawnerTop random potentialcelltop]
late [CellSpawnerBottom no BloodCellBottom] -> [CellSpawnerBottom random potentialcellbottom]

[stationary BloodCell] -> [right BloodCell]



(bloodcell movement mechanics)
right [> BloodCellTop | Obstacle ] -> [up BloodCellTop | Obstacle]
right [> BloodCellBottom | Obstacle ] -> [down BloodCellBottom | Obstacle]

up [> BloodCellTop | Obstacle] -> [down BloodCellBottom | Obstacle]
down [> BloodCellBottom | Obstacle] -> [up BloodCellTop | Obstacle]




[BloodCell CellKiller] -> [CellKiller]
late [BloodCell Virus] -> [BloodCell]

[Virus1] -> [Virus2]
[Virus3] -> [Virus1]
[Virus2] -> [Virus3]







==============
WINCONDITIONS
==============

no Virus

=======
LEVELS
=======

MESSAGE Kill the virus! Direct white blood cells into the viruses by placing obstacles in their path.

##########
#..@.....#
#.@p@....#
#..@.g...#
KssssssssH
LssssssssH
#....g...#
#........#
#........#
##########

MESSAGE The virus is spreading! 

############
#.......#g.#
#.@........#
#@p@....#g.#
#.@......###
KssssssssssH
LssssssssssH
#.....#....#
#....#g..#g#
#.........##
#..........#
############

MESSAGE You've found the source of the virus! They've almost been cleared out!

##############
#...###..#...#
#...ggg#.#g..#
#gg###g..#...#
#@p##...##...#
#.@##...#....#
KssssssssssssH
LssssssssssssH
##g####g###g##
#............#
####g.###..g.#
#####....#.###
##########.g##
##############

MESSAGE The virus is clear! Good work.