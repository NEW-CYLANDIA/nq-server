title Well
author Le Slo
homepage https://le-slo.itch.io/
impact well


noaction

key_repeat_interval 5.15
realtime_interval 0.1
zoomscreen 7x15

norepeat_action
noundo
norestart

run_rules_on_level_start


========
OBJECTS
========


Sky
#57a9df #17496f
11111
11111
11111
11111
11111


Background
#53a0df #17496f
11111
11111
11111
11111
00000

Water
#57a9df

Water1
#53a0df #62a9df #57a3d4
00000
10001
11111
11111
22111

Water2
#53a0df #62a9df #57a3d4
22222
22222
00222
00000
00000

Water3
#53a0df #62a9df #57a3d4
11100
11111
22112
22222
22200

Wall
#e9ae71 #e98e51 #57a9df
.100.
11100
01100
00000
00000

Player0
#e98e51 #e9ae71 #424063 #a6d271
.000.
.111.
11111
.223.
.1.1.

Player0Bis
#e98e51 #e9ae71 #424063 #a6d271
.000.
11111
.111.
.223.
1...1

Player0NormalClimb0
#e98e51 #e9ae71 #424063 #a6d271
.000.
.1111
1111.
.223.
...1.

Player0NormalClimb1
#e98e51 #e9ae71 #424063 #a6d271
.000.
1111.
.1111
.223.
.1...

Player0HealthyClimb0
#e98e51 #f2be91 #424063 #a6d271
.000.
.1111
1111.
.223.
...1.


PlayerUp0
#e98e51 #f9dea1 #424063 #a6d271
.111.
11111
.223.
.1.1.
.....

Player0HealthyA
#e98e51 #f2be91 #424063 #a6d271
.1111
.223.
.1...
.....
.....

Player0HealthyB
#e98e51 #f2be91 #424063 #a6d271
.....
.....
.....
.000.
1111.



Player0TiredClimb0
#e98e51 #ffeeb9 #424063 #a6d271
.000.
.1111
1111.
.223.
...1.

Player0TiredA1
#e98e51 #ffeeb9 #424063 #a6d271
.....
.....
.....
.000.
1111.

Player0TiredA2
#e98e51 #ffeeb9 #424063 #a6d271
.1111
.223.
.1...
.....
.....


Player0TiredB1
#e98e51 #ffeeb9 #a66271 #424063
.....
.....
.000.
.1111
1111.

Player0TiredB2
#e98e51 #ffeeb9 #424063 #a6d271
.223.
...1.
.....
.....
.....


Player0TiredC1
#e98e51 #ffeeb9 #424063 #a6d271
.....
.000.
1111.
.1111
.223.

Player0TiredC2
#e98e51 #ffeeb9 #424063 #a6d271
.1...
.....
.....
.....
.....




Player0ExhaustedClimb0
#e98e51 #ffeeb9 #424063 #a6d271
.000.
.1111
1111.
.223.
...1.

Player0ExhaustedA1
#e98e51 #ffeeb9 #424063 #a6d271
.....
.....
.....
.....
.000.

Player0ExhaustedA2
#e98e51 #ffeeb9 #424063 #a6d271
.111.
11111
.223.
.1.1.
.....



Player0ExhaustedB1
#e98e51 #ffeeb9 #a66271 #424063
.....
.....
.....
.....
.000.

Player0ExhaustedB2
#e98e51 #ffeeb9 #424063 #a6d271
1111.
.1111
.223.
...1.
.....


Player0ExhaustedC1
#e98e51 #ffeeb9 #424063 #a6d271
.....
.....
.....
.000.
.111.

Player0ExhaustedC2
#e98e51 #ffeeb9 #424063 #a6d271
11111
.223.
.1.1.
.....
.....


Player0ExhaustedD1
#e98e51 #ffeeb9 #424063 #a6d271
.....
.....
.....
.000.
.1111

Player0ExhaustedD2
#e98e51 #ffeeb9 #424063 #a6d271
1111.
.223.
...1.
.....
.....


Player0ExhaustedE1
#e98e51 #ffeeb9 #a66271 #424063
.....
.....
.000.
.111.
11111

Player0ExhaustedE2
#e98e51 #ffeeb9 #424063 #a6d271
.223.
.1.1.
.....
.....
.....


Player0ExhaustedF1
#e98e51 #ffeeb9 #424063 #a6d271
.....
.....
.000.
1111.
.1111

Player0ExhaustedF2
#e98e51 #ffeeb9 #424063 #a6d271
.223.
.1...
.....
.....
.....

Player0ExhaustedG1
#e98e51 #ffeeb9 #424063 #a6d271
.....
.000.
.1111
1111.
.223.

Player0ExhaustedG2
#e98e51 #ffeeb9 #424063 #a6d271
.1...
.....
.....
.....
.....


Player1
#e98e51 #f9dea1 #424063 #a6d271
.000.
1111.
.1111
.223.
.1...

PlayerUp1
#e98e51 #f9dea1 #424063 #a6d271
.111.
11111
.223.
.1.1.
.....

Player2
#e98e51 #ffeeb9 #424063 #a6d271
.000.
.1111
1111.
.223.
.1...

PlayerUp2
#e98e51 #ffeeb9 #424063 #a6d271
.111.
11111
.223.
.1.1.
.....

Player3
#e98e51 #ffeedf #424063 #a6d271
.000.
1111.
.1111
.223.
.1...

PlayerUp3
#e98e51 #ffeedf #424063 #a6d271
.111.
11111
.223.
.1.1.
.....

Player4
#e98e51 #efdeef #424063 #a6d271
.000.
.1111
1111.
.223.
.1...

PlayerUp4
#e98e51 #efdeef #424063 #a6d271
.111.
11111
.223.
.1.1.
.....

Player5
#e98e51 #cfbeef #424063 #a6d271
.000.
1111.
.1111
.223.
.1...

PlayerUp5
#e98e51 #cfbeef #424063 #a6d271
11111
.223.
.1.1.
.....
.....

PlayerHair
#e98e51 #53a0df
.....
.....
.....
11111
10001


PlayerHairSplash
#e98e51 #cfbeef #53a0df
....2
2..2.
.2...
20002
21112

Ladder
#343745
.0.0.
.000.
.0.0.
.000.
.0.0.

Bubble
#6abdef
.000.
0..00
0...0
0...0
.000.


counterstart
transparent

counterend
transparent

tick
transparent

counter
transparent



algae0
#6abd7f
.....
...0.
...0.
..00.
...00

algae1
#6abd7f #2a9d5f
...01
..00.
..10.
...00
...00

algae2
#6abd7f #2a9d5f
..00.
..100
...01
...0.
...00

fish0R
#ef9d3f #123452
.....
.00.0
01000
00000
.00.0

fish1R
#cf8d8f #123452
.....
..0.0
.0000
..0.0
.....

fish2R
#7f5d7f
.....
.....
..00.
.....
.....

fish0L
#ef9d3f #123452
0.00.
00010
00000
0.00.
.....

fish1L
#cf8d8f #123452
0.0..
0000.
0.0..
.....
.....

fish2L
#7f5d7f
...00
.....
.....
.....
.....

Wallmetal
#354653
00000
00000
00000
00000
00000


limit
black

start
transparent

swimmingCounter0
transparent

swimmingCounter1
transparent

swimmingCounter2
transparent

swimmingCounter3
transparent

air0
transparent

air1
transparent

air2
transparent

air3
transparent

warning0
transparent

warning1
transparent

warning2
transparent

warning3
transparent

warning4
transparent

warning5
transparent

warning6
transparent

warning7
transparent

warning8
transparent

warning9
transparent

warning10
transparent

warning11
transparent

warning12
transparent

warning13
transparent

warning14
transparent

mult0
transparent

mult1
transparent

mult2
transparent

mult3
transparent

=======
LEGEND
=======

. = Background
- = water
* = Wall and counterstart
? = wall and counterend
O = Wall
# = Ladder

0 = Ladder and air0
1 = Ladder and air1
2 = Ladder and air2
3 = Ladder and air3

P = Player0 and water and start
S = sky
X = limit
% = Wallmetal

algaeTop = algae0
algaes = algae1 or algae2
counters = counterend or counter

waters = Water1 or Water2 or Water3

(playerstate)
PlayerDrowning = Player1 or Player2 or Player3 or Player4 or Player5
PlayerHealthy = Player0HealthyA or Player0HealthyB or Player0HealthyClimb0
PlayerTired = Player0TiredClimb0 or Player0TiredA1 or Player0TiredA2 or Player0TiredB1 or Player0TiredB2 or Player0TiredC1 or Player0TiredC2
PlayerExhausted = Player0ExhaustedClimb0 or Player0ExhaustedA1 or Player0ExhaustedA2 or Player0ExhaustedB1 or Player0ExhaustedB2 or Player0ExhaustedC1 or Player0ExhaustedC2 or Player0ExhaustedD1 or Player0ExhaustedD2 or Player0ExhaustedE1 or Player0ExhaustedE2 or Player0ExhaustedF1 or Player0ExhaustedF2 or Player0ExhaustedG1 or Player0ExhaustedG2


Player = Player0Bis or Player0 or Player0NormalClimb0 or Player0NormalClimb1 or PlayerHealthy or PlayerTired or PlayerDrowning or playerexhausted


PlayerUps = PlayerUp0 or PlayerUp1 or PlayerUp2 or PlayerUp3 or PlayerUp4 or PlayerUp5

SwimmingCounters = swimmingCounter0 or swimmingCounter1 or swimmingCounter2 or swimmingCounter3

fishR = fish0R or fish1R or fish2R
fishL = fish0L or fish1L or fish2L


warnings = warning0 or warning1 or warning2 or warning3 or warning4 or warning5 or warning6 or warning7 or warning8 or warning9 or warning10 or warning11 or warning12 or warning13 or warning14

mult = mult0 or mult1 or mult2 or mult3

=======
SOUNDS
=======

startgame 22961308

showmessage 72760508
closemessage 52727708
endlevel 33763308

Player0Bis create 50881107
PlayerHair create 60950904
PlayerHair create 78288306
PlayerHair create 36444308

Player1 create 19615505
Player2 create 19615505
Player3 create 38791505
Player4 create 38791505
Player5 create 93603305

Bubble create 10223507

Player0NormalClimb0 create 19422907
Player0NormalClimb1 create 6300107

sfx2 19586308

sfx3 79774708
================
COLLISIONLAYERS
================

Background

mult
warnings

air0, air1, air2, air3
start
Sky
Water, Waters
swimmingCounters



fish2R
fish2L

algae1

fish1R


fish1L
fish0L

algae2, algae0


fish0R

Ladder
Bubble

PlayerUps, PlayerHair, PlayerHairSplash
Player, Wall, limit, Wallmetal
counterstart, counterend
counter
tick

======
RULES
======

(-----------------Start-----------)

[start][wall no tick] -> [start][wall tick]


down[start][no ladder | ladder] -> [start][ no ladder| ladder water1]

down[start][water1 | no waters] -> [start][water1 | water2] 
+ down[start][water2 | no waters] -> [start][water2 | water3]
+ down[start][water3 | no waters] -> [start][water3 | water1]

horizontal[start][water1 | no waters] -> [start][water1 | water1] 
horizontal[start][water2 | no waters] -> [start][water2 | water2] 
horizontal[start][water3 | no waters] -> [start][water3 | water3]


startloop 
down random[start][wall tick][waters no Wallmetal | waters] -> [start][wall][waters | waters algaetop]
endloop
down[start][algaetop | no algaes] -> [start][algaetop | random algaes] 
down[start][algaes | no algaes] -> [start][algaes | random algaes] 


up[start][ladder | no ladder] -> [start][ladder | ladder]

[start] -> []

(-----------------Playing with counters-----------)

(when moving player, counter restarts)
[up player][counterend] -> [up player][counterend counter]
[counterstart]->[counterstart tick]

(filling the counter)
right[counterend counter][ no counters | counter] -> [counterend counter][ counter | counter]


[tick] [counterend counter] -> [][counterend]
right [tick] [counter | no counter] -> [] [|]



[tick] -> []

(------------------------- ladder -----------------------)

[player] -> [player tick]

(Swimming)

[swimmingCounter2] -> []
[swimmingCounter1]->[]
[swimmingCounter0]->[swimmingCounter1]


[player0Bis tick][counterstart no swimmingcounters] -> [ player0][counterstart ]
[moving player0Bis tick][counterstart swimmingcounters] -> [moving player0][counterstart ]

[moving player0 tick][counterstart swimmingcounters] -> [moving player0Bis][counterstart]
[moving player0 tick][counterstart no swimmingcounters] -> [moving player0Bis][counterstart swimmingcounter0]

(Normal climb)

[moving player0NormalClimb0 tick ladder] -> [moving player0NormalClimb1 ladder]
[moving player0NormalClimb1 tick ladder] -> [moving player0NormalClimb0 ladder]


[Player0NormalClimb0 no ladder] -> [Player0]
[Player0NormalClimb1 no ladder] -> [Player0]


(Healthy climb)

up [up Player0HealthyClimb0 ladder | ladder] -> [Player0HealthyA ladder | player0HealthyB ladder]
up [up Player0HealthyA ladder | up Player0HealthyB ladder] -> [ladder | Player0HealthyClimb0 Bubble ladder]


[down playerhealthy] -> [playerhealthy]
[down playertired] -> [playertired]
[down playerexhausted] -> [playerexhausted]
[down playerdrowning] -> [playerdrowning]

(down [down Player0HealthyClimb0 ladder | ladder] -> [Player0HealthyB ladder | player0HealthyA ladder]
down [down Player0HealthyB ladder | down Player0HealthyA ladder] -> [ ladder | Player0HealthyClimb0 ladder])


(Tired climb)

up [up Player0TiredClimb0 ladder | ladder waters] -> [Player0TiredA2 Bubble ladder | player0TiredA1 ladder water]
up [up Player0TiredA2 ladder | up player0TiredA1 ladder] -> [ Player0TiredB2 Bubble ladder |  player0TiredB1 ladder]
up [up Player0TiredB2 ladder | up player0TiredB1 ladder] -> [ Player0TiredC2 Bubble ladder |  player0TiredC1 ladder]
up [up Player0TiredC2 ladder | up player0TiredC1 ladder] -> [ ladder | Player0TiredClimb0 Bubble ladder]


(Exhausted climb)

up [up Player0ExhaustedClimb0 ladder | ladder waters] -> [Player0ExhaustedA2 Bubble ladder | player0ExhaustedA1 ladder water]
up [up Player0ExhaustedA2 ladder | up player0ExhaustedA1 ladder] -> [ Player0ExhaustedB2 Bubble ladder |  player0ExhaustedB1 ladder]
up [up Player0ExhaustedB2 ladder | up player0ExhaustedB1 ladder] -> [ Player0ExhaustedC2 Bubble ladder |  player0ExhaustedC1 ladder]
up [up Player0ExhaustedC2 ladder | up player0ExhaustedC1 ladder] -> [ Player0ExhaustedD2 Bubble ladder |  player0ExhaustedD1 ladder]
up [up Player0ExhaustedD2 ladder | up player0ExhaustedD1 ladder] -> [ Player0ExhaustedE2 Bubble ladder |  player0ExhaustedE1 ladder]
up [up Player0ExhaustedE2 ladder | up player0ExhaustedE1 ladder] -> [ Player0ExhaustedF2 Bubble ladder |  player0ExhaustedF1 ladder]
up [up Player0ExhaustedF2 ladder | up player0ExhaustedF1 ladder] -> [ Player0ExhaustedG2 Bubble ladder |  player0ExhaustedG1 ladder]
up [up Player0ExhaustedG2 ladder | up player0ExhaustedG1 ladder] -> [ ladder | Player0ExhaustedClimb0 Bubble ladder]



(------------------------- air ---------------------------)

late [Player air0] -> [Player0NormalClimb0 air0]

late [Player0NormalClimb0 air1] -> [Player0HealthyClimb0 Bubble] sfx3

late [Player0HealthyClimb0 air2] -> [Player0TiredClimb0 Bubble]

late [Player0TiredClimb0 air3] -> [Player0ExhaustedClimb0 Bubble warning0]




[tick] -> []

(-----------------Fish-----------)

(fish creating)
random left [counterstart no tick][limit | waters no wall] -> [counterstart no tick][limit random fishR| waters]
+ right [counterstart no tick][limit | waters no wall] -> [counterstart no tick][limit random fishL| waters]

[stationary fishR] -> [left fishR]
late [fishR limit] -> [limit]

[stationary fishL] -> [right fishL]
late [fishL limit] -> [limit]

[fishR Wallmetal] -> [Wallmetal]
[fishL Wallmetal] -> [Wallmetal]


up [stationary player][Bubble | waters] -> [stationary player][> bubble | waters]

(------sinking------)
down [counterend no counter][counterstart no counter][stationary player no ladder | no wall] -> [counterend counter][counterstart][down player Bubble |]


up [Bubble | no waters] -> [ | ]


[playerUp0 | PlayerHair] -> [Player1 | ]
[playerUp1 | PlayerHair] -> [Player2 | ]
[playerUp2 | PlayerHair] -> [Player3 | ]
[playerUp3 | PlayerHair] -> [Player4 | ]
[playerUp4 | PlayerHair] -> [player5 | ] 


up[ up player no ladder | no waters] -> [player | ]

up[ up player0ExhaustedClimb0 ladder | no waters] -> [ PlayerUp0 ladder | PlayerHair]again 
up[ up player1 ladder | no waters] -> [ PlayerUp1 ladder | PlayerHair]again
up[ up player2 ladder | no waters] -> [ PlayerUp2 ladder | PlayerHair]again
up[ up player3 ladder | no waters] -> [ PlayerUp3 ladder | PlayerHair]again
up[ up player4 ladder | no waters] -> [ PlayerUp4 ladder | PlayerHair]again
up[ up player5 ladder | no waters] -> [ PlayerUp5 ladder | PlayerHairSplash]win


(-----------------warning sounds------------------------)

(setting mult)

late [warnings]->[warnings tick]

late [warnings no mult] -> [warnings mult3]

late [tick] -> []

late [warnings]->[warnings tick]

late [warnings mult3 tick] -> [warnings mult2]
late [warnings mult2 tick] -> [warnings mult1]
late [warnings mult1 tick] -> [warnings mult0]

late [warning5 mult0 tick] -> [warning4 mult3]
late [warning4 mult0 tick] -> [warning3 mult3]
late [warning3 mult0 tick] -> [warning2 mult3]
late [warning2 mult0 tick] -> [warning1 mult3]
late [warning1 mult0 tick] -> [warning0 mult3]

late [warning0 tick] -> [warning5] sfx2

late [tick] -> []


==============
WINCONDITIONS
==============



=======
LEVELS
=======

(Short tests)

(xsssssssssssssx
xsssssssssssssx
xsssssssssssssx
x.............x
x%%%%%.#.%%%%%x
x%%%%%.#.%%%%%x
x%%%%%.#.%%%%%x
x%%%%%.3.%%%%%x
x%%%%%.3.%%%%%x
x%%%%%.3.%%%%%x
x%%%%%.#.%%%%%x
x%%%%%.2.%%%%%x
x%%%%%.#.%%%%%x
x%%%%%.1.%%%%%x
x%%%%%.#.%%%%%x
x%%%%%.0.%%%%%x
x.............x
x......p......x
x*ooooooooooo?x)


xsssssssssssssx
x.............x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%3%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%2%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%1%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%%#%%%%%%x
x%%%%%.0.%%%%%x
x%%%%.....%%%%x
x%%%.......%%%x
x%%.........%%x
x%...........%x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x.............x
x......p......x
xooo*oooo?oooox

